#version 450

struct Particle{
    vec3 Location;
    vec3 Velocity;
    vec3 DeltaLocation;
    float Lambda;
    float Density;
    float Mass;

    vec3 TmpVelocity;

    uint CellHash;
    uint TmpCellHash;


    uint NumNgbrs;
};

layout(binding=0) uniform UniformNSObject{
    uint numParticles;
    uint workgroup_count;
    uint hashsize;
    float sphRadius;
}; 
layout(binding=2) buffer OutIndexbuffer{
    uint outindex[];
};
layout(binding=3) buffer ParticleBuffer{
    Particle particles[];
};
layout(binding=4) buffer ParticleNgbrBuffer{
    uint particlengbrs[];
};
layout(binding=6) buffer CellinfoBuffer{
    uint cellinfo[];
};
layout(binding=7) buffer LocalPrefixBuffer{
    uint localprefix[];
};

layout(local_size_x=512,local_size_y=1,local_size_z=1) in;
void main(){
    uint particleindex = gl_GlobalInvocationID.x;
     if(particleindex<numParticles){
        particles[particleindex].NumNgbrs = 0;
        int i0 = int(floor(particles[particleindex].Location.x/sphRadius));
        int j0 = int(floor(particles[particleindex].Location.y/sphRadius));
        int k0 = int(floor(particles[particleindex].Location.z/sphRadius));

        for(int di=-1;di<=1;++di){
            for(int dj=-1;dj<=1;++dj)
                for(int dk=-1;dk<=1;++dk){
                    int i = i0 + di;
                    int j = j0 + dj;
                    int k = k0 + dk;
                    uint hashvalue = (uint((73856093*i)^(19349663*j)^(83492791*k)))%hashsize;
                    uint begin = cellinfo[2*hashvalue];
                    uint end = cellinfo[2*hashvalue+1];
                    for(uint idx=begin;idx!=end;++idx){
                        uint ngbr = outindex[idx];
                        if(ngbr != particleindex){
                            if(length(particles[ngbr].Location - particles[particleindex].Location)<sphRadius&&particles[particleindex].NumNgbrs<128){
                                particlengbrs[128*particleindex+particles[particleindex].NumNgbrs] = ngbr;
                                particles[particleindex].NumNgbrs += 1;
                            }
                        }
                    }
                }
        }
    }
}